allprojects { Project project ->
    // make 'build/generated-sources/*' optional source directories
    project.plugins.matching { it == project.plugins.findPlugin('eclipse') }.all {
        project.plugins.matching { it == project.plugins.findPlugin('asakusafw') }.all {
            logger.info "Configuring Eclipse for Asakusa Framework: ${project.name}"
            def conv = project.asakusafw
            project.eclipse.classpath {
                file.withXml { provider ->
                    provider.asNode().children().findAll{
                        it.name() == 'classpathentry' \
                        && it.@kind == 'src' \
                        && it.@path \
                        && ( conv.modelgen.modelgenSourceDirectory && it.@path == project.relativePath(conv.modelgen.modelgenSourceDirectory)
                          || conv.javac.annotationSourceDirectory && it.@path == project.relativePath(conv.javac.annotationSourceDirectory))
                    }.collect {
                        it.children().find { it.name() == 'attributes' } ?: it.appendNode('attributes')
                    }.collect {
                        it.children().find { it.name() == 'attribute' && it.@name == 'optional' } ?: it.appendNode('attribute', [name: 'optional'])
                    }.each {
                        it.@value = 'true'
                    }
                }
            }
        }
    }
}