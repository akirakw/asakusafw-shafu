defaultTasks 'cleanLibraries', 'copyLibraries'

def gradleVersion = '2.5'

def classpathDestinationDir = file('lib')
def sourceDestinationDir = file('libsrc')

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }
}

configurations {
    pluginClasspath
    pluginSources
}

dependencies {
    pluginClasspath "org.gradle:gradle-tooling-api:${gradleVersion}"
    pluginClasspath "org.slf4j:slf4j-simple:1.7.10"
    pluginClasspath "org.slf4j:jcl-over-slf4j:1.7.10"
    pluginClasspath("org.apache.commons:commons-compress:1.6") {
        exclude module: 'xz'
        exclude module: 'commons-logging'
    }
    pluginClasspath("org.apache.httpcomponents:httpclient:4.3.1") {
        exclude module: 'commons-logging'
    }
    pluginSources "org.gradle:gradle-tooling-api:${gradleVersion}:sources"
}

task cleanLibraries << {
    delete classpathDestinationDir
}

task copyLibraries << {
    copy {
        from files(configurations.pluginClasspath)
        into classpathDestinationDir
    }
}
